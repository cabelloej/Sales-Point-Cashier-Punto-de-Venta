***
DEFI WIND BRSHOW FROM 03,00 TO 16,79;
                TITLE  " Articulos ";
                DOUBLE NOFLOAT NOZOOM NOGROW SHADOW COLOR SCHEME 10
***
DEFI WIND BRGET FROM 17,00 TO 22,79;
                FOOTER "F3=Eliminar,F6=Abrir caja,F7=Articulos,F10=Cant,F11=Prec,F12=Imprimir,Esc=Salir "
***
ON KEY LABEL F1
ON KEY LABEL F2
ON KEY LABEL F3 DO BRELI
ON KEY LABEL F4
ON KEY LABEL F5
ON KEY LABEL F6 DO ABRECAJA
ON KEY LABEL F7 DO ARTICULO
ON KEY LABEL F8
ON KEY LABEL F9
ON KEY LABEL F10 DO BREDIC
ON KEY LABEL F11 DO BREDIP
ON KEY LABEL F12
***
*** INICIO DE FACTURACION
***
CLOSE DATA
CLOSE INDEX
***
SELECT 15
USE PTODATA
DO OPELIN
DO OPEART
DO OPEVTAG
DO OPEVTAD
***
STORE SPACE(LEN(CODIGO)) TO WCODIGO
STORE 0                  TO WRENGLON
STORE SPACE(1)           TO WRENGLONC
STORE 0                  TO WFACTURA
STORE SPACE(1)           TO WFACTURAC
STORE .F.                TO WFLAGEDITC
STORE .F.                TO WFLAGEDITP
STORE 0                  TO WTOTFAC
STORE 0                  TO WEFECTIVO
STORE 0                  TO WCAMBIO
STORE 1                  TO WSAL
DO SETPRI
DO INIVTA
DO GETVTA
DO RESETPRI
DO CLOSEVTA
RETURN

*****************
PROCEDURE INIVTA
*****************
SELECT SYSVTAG
STORE ALLTRIM(WUSERCODE)+".DG1"   TO WVTADG1
STORE ALLTRIM(WUSERCODE)+".IG1"   TO WVTAIG1
STORE ALLTRIM(WUSERCODE)+".IG2"   TO WVTAIG2
STORE ALLTRIM(WUSERCODE)+".IG3"   TO WVTAIG3
COPY STRU TO &WVTADG1
SELECT 10
USE &WVTADG1 ALIAS TEMVTA1
IF FILLOC()
   INDEX ON FACTURA               TO &WVTAIG1
   INDEX ON CLIENTE               TO &WVTAIG2
   INDEX ON STR(YEAR(FECHA),4)+;
            STR(MONTH(FECHA),2)+;
            STR(DAY(FECHA),2)     TO &WVTAIG3
ELSE
   STORE "OPERACION RECHAZADA, REINTENTE" TO WTEXT
   DO AVISO WITH WTEXT
   CLOSE DATA
   CLOSE INDEX
   RETURN
ENDIF
***
SELECT SYSVTAD
STORE ALLTRIM(WUSERCODE)+".DD1" TO WVTADD1
STORE ALLTRIM(WUSERCODE)+".ID1" TO WVTAID1
COPY STRU TO &WVTADD1
SELECT 11
USE &WVTADD1 ALIAS TEMVTA2
INDEX ON FACTURA+RENGLON        TO &WVTAID1
SET RELA TO CODIGO              INTO SYSART

STORE 0 TO WTOTFAC

***
***
RETURN
****************
PROCEDURE GETVTA
****************
DO WHILE .T.
   ***
   SELECT TEMVTA2
   DO VENDIDO
   ***
   ACTI WIND BRGET
   STORE SPACE(25) TO WCLIENTE
   STORE WRENGLON + 1 TO WRENGLON
   @ 0,0 CLEAR
   @ 0,40 SAY "Total   :"
   @ 1,40 SAY "Efectivo:"
   @ 2,40 SAY "Cambio  :"
   @ 3,40 SAY "Cliente :"
   @ 0,53 SAY WTOTFAC PICTURE "99,999,999.99"
   DO SHOWEDIT
   ON KEY LABEL F12 DO BRFAC
   @ 0,00 GET WCODIGO
   READ
   ?? CHR(7)
   ON KEY LABEL F12
   IF LASTKEY()=27 .AND. WSAL=0
      EXIT
   ELSE
      STORE 0 TO WSAL
   ENDIF
   IF WCODIGO = SPACE(LEN(CODIGO))
      LOOP
   ENDIF
   SELECT SYSART
   SEEK WCODIGO
   IF .NOT. FOUND()
      LOOP
   ENDIF
   @ 0,00 SAY SYSART.DESCRI
   STORE SYSART.DESCRI    TO WDESCRI
   STORE SYSART.DESCRI    TO WUNIDAD
   STORE 1                TO WCANTIDAD
   STORE SYSART.PRECIO    TO WPRECIO
   STORE SYSART.IMPUESTO1 TO WIMPUESTO1
   STORE SYSART.IMPUESTO2 TO WIMPUESTO2
   IF WFLAGEDITC
      @ 0,33 CLEAR TO 0,78
      @ 0,33 GET WCANTIDAD PICTURE "999.999"
      READ
   ELSE
      @ 0,33 SAY WCANTIDAD PICTURE "999.999"
   ENDIF
   IF WFLAGEDITP
      @ 0,40 CLEAR TO 0,78
      @ 0,43 GET WPRECIO PICTURE "999,999.99"
      READ
   ELSE
      @ 0,43 SAY WPRECIO PICTURE "999,999.99"
   ENDIF
   STORE WCANTIDAD*WPRECIO TO WTOTAL
   @ 0,53 SAY WTOTAL PICTURE "99,999,999.99"
   ?? CHR(7)
   SELECT TEMVTA2
   IF FILLOC()
      APPEND BLANK
      UNLOCK ALL
      IF RECLOC()
         DO ARMAREN
         REPLACE RENGLON   WITH WRENGLONC
         REPLACE CODIGO    WITH WCODIGO
         REPLACE CANTIDAD  WITH WCANTIDAD
         REPLACE PRECIO    WITH WPRECIO
         REPLACE TOTAL     WITH WTOTAL
         REPLACE IMPUESTO1 WITH WIMPUESTO1
         REPLACE IMPUESTO2 WITH WIMPUESTO2
         STORE WTOTFAC+WTOTAL TO WTOTFAC
      ELSE
         STORE "Rechazado el ingreso del articulo, favor reintentar. oprima <ENTER>" to wtext
         do aviso with wtext
      ENDIF
   ELSE
      STORE "Rechazado el ingreso del articulo, favor reintentar. oprima <ENTER>" to wtext
      do aviso with wtext
   ENDIF
ENDDO
RETURN
***************
PROCEDURE BREDIC
***************
IF WFLAGEDITC
   STORE .F. TO WFLAGEDITC
   @ 01,00 SAY "          "
ELSE
   STORE .T. TO WFLAGEDITC
   @ 01,00 SAY "Cantidad  "
ENDIF
RETURN
***************
PROCEDURE BREDIP
***************
IF WFLAGEDITP
   STORE .F. TO WFLAGEDITP
   @ 02,00 SAY "          "
ELSE
   STORE .T. TO WFLAGEDITP
   @ 02,00 SAY "Precios   "
ENDIF
RETURN
******************
PROCEDURE SHOWEDIT
******************
IF WFLAGEDITC
   @ 01,00 SAY "Cantidad  "
ELSE
   @ 01,00 SAY "          "
ENDIF
IF WFLAGEDITP
   @ 02,00 SAY "Precio    "
ELSE
   @ 02,00 SAY "          "
ENDIF
RETURN
***************
PROCEDURE BRFAC
***************
DO GETCAM
DO ABRECAJA
IF WCLIENTE<>SPACE(25)
   DO IMPFAC
ENDIF
STORE 0 TO WTOTFAC
STORE 0 TO WRENGLON
STORE SPACE(LEN(SYSART.CODIGO)) TO WCODIGO
DO GRAFAC
RETURN
******************
PROCEDURE CLOSEVTA
******************
CLOSE DATA
CLOSE INDEX
SET DEVI TO SCRE
RELEASE WIND BRSHOW
RELEASE WIND BRGET
ON KEY LABEL F1
ON KEY LABEL F2
ON KEY LABEL F3
ON KEY LABEL F4
ON KEY LABEL F5
ON KEY LABEL F6
ON KEY LABEL F7
ON KEY LABEL F8
ON KEY LABEL F9
ON KEY LABEL F10
ON KEY LABEL F11
ON KEY LABEL F12
RETURN
******************
PROCEDURE ABRECAJA
******************
IF PTODATA.TIPCAJA<>" "
   IF PTODATA.TIPCAJA="S"
      *** ABRE SERIAL
      IF PTODATA.NUMCAJA="1"
         !dir  abre.dbf>COM1
      ELSE
         !dir  abre.dbf>COM2
      ENDIF
   ELSE
      *** ABRE PARALELO
      IF PTODATA.NUMCAJA="1"
         !type abre.dbf>LPT1
      ELSE
         !type abre.dbf>LPT2
      ENDIF
   ENDIF
ENDIF
RETURN
****************
PROCEDURE IMPFAC
****************
SET DEVI TO PRINT
@ PROW()+0,00 SAY CHR(18)
@ PROW()+0,00 SAY QQWW
@ PROW()+0,00 SAY CHR(15)
@ PROW()+1,00 SAY PTODATA.TOPFAC1
@ PROW()+1,00 SAY PTODATA.TOPFAC2
@ PROW()+1,00 SAY PTODATA.TOPFAC3
@ PROW()+1,00 SAY "FACTURA # "+WFACTURAC
@ PROW()+1,00 SAY "Cliente: "+wcliente
@ PROW()+1,00 SAY "Fecha:"+dtoc(date())+"           Hora:"+time()
@ PROW()+2,00 SAY "Articulo"
@ PROW()+0,21 SAY "Cantd."
@ PROW()+0,31 SAY "SubTotal"
@ PROW()+1,00 SAY REPLICATE("-",40)
SELECT TEMVTA2
GO TOP
DO WHILE .NOT.EOF()
   @ PROW()+1,00 SAY SUBSTR(SYSART.DESCRI,1,20)
   @ PROW()+0,21 SAY TEMVTA2.CANTIDAD PICTURE "999.99"
   @ PROW()+0,29 SAY TEMVTA2.TOTAL    PICTURE "999,999.99"
   IF IMPUESTO1+IMPUESTO2=0
      @ PROW()+0,39 SAY "E"
   ENDIF
   SELECT TEMVTA2
   SKIP
ENDDO
@ PROW()+1,00 SAY REPLICATE("-",40)
@ PROW()+1,13 SAY "TOTAL FACTURA:"+STR(WTOTFAC,12,2)
@ PROW()+1,13 SAY "EFECTIVO     :"+STR(WEFECTIVO,12,2)
@ PROW()+1,13 SAY "SU CAMBIO    :"+STR(WCAMBIO,12,2)
@ PROW()+3,00 SAY PTODATA.PIEFAC1
@ PROW()+1,00 SAY PTODATA.PIEFAC2
@ PROW()+15,00 say "."
@ PROW()+1,00 say " "
SET DEVI TO SCRE
RETURN
****************
PROCEDURE GRAFAC
****************
*** GRABA REG EN SYSVTAG
SELECT SYSVTAG
IF FILLOC()
  APPEND BLANK
  UNLOCK ALL
  DO RECLOC
  REPLACE FACTURA WITH WFACTURAC
  REPLACE FECHA   WITH DATE()
  REPLACE CLIENTE WITH WCLIENTE
  UNLOCK ALL
ELSE
   DO CLOSEVTA
   RETURN TO MASTER
ENDIF
*** GRABA RENGLONES EN SYSVTAD
SELECT TEMVTA2
GO TOP
DO WHILE .NOT. EOF()
   SCAT MEMVAR
   SELECT SYSVTAD
   IF FILLOC()
      APPEND BLANK
      GATH MEMVAR
      REPLACE FACTURA WITH WFACTURAC
      REPLACE FECHA   WITH DATE()
      UNLOCK ALL
   ELSE
      DO CLOSEVTA
      RETURN TO MASTER
   ENDIF
   SELECT TEMVTA2
   SKIP
ENDDO
*** BORRA EL ARCHIVO TEMPORAL TEMVTA2
SELECT TEMVTA2
IF FILLOC()
   DELETE ALL
   *PACK
   UNLOCK ALL
ELSE
   DO CLOSEVTA
   RETURN TO MASTER
ENDIF
RETURN
****************
PROCEDURE GETCAM
****************
DO WHILE .T.
   STORE WTOTFAC TO WEFECTIVO
   @ 01,53 GET WEFECTIVO RANGE 0.01, 99999999.99 PICTURE "99,999,999.99"
   READ
   IF WEFECTIVO>=WTOTFAC
      EXIT
   ELSE
      STORE "EFECTIVO INCOMPLETO." TO WTEXT
      DO AVISO WITH WTEXT
   ENDIF
ENDDO
STORE WEFECTIVO-WTOTFAC TO WCAMBIO
@ 02,53 SAY WCAMBIO PICTURE "99,999,999.99"
@ 03,53 GET WCLIENTE
READ
SELECT PTODATA
IF RECLOC()
   REPLACE FACTURA WITH FACTURA+1
   STORE FACTURA TO WFACTURA
   UNLOCK ALL
ELSE
   DO CLOSEVTA
   RETURN TO MASTER
ENDIF
DO ARMAFAC
SELECT TEMVTA2
IF FILLOC()
   REPLACE ALL FACTURA WITH WFACTURAC
   UNLOCK ALL
ENDIF
***
RETURN
****************
PROCEDURE BRELI
***************
CLEAR GETS
CLEAR TYPEAHEAD
ON KEY LABEL F3 DO BRELI2
ACTI WIND BRSHOW
SELECT TEMVTA2
BROWSE FIELDS SYSART.CODIGO:H="CODIGO",SYSART.DESCRI:H="Articulo",TEMVTA2.CANTIDAD:H="Cantidad",TEMVTA2.PRECIO:H="Precio",TEMVTA2.TOTAL:H="Subtotal"
ON KEY LABEL F3 DO BRELI
STORE 1 TO WSAL
STORE SPACE(LEN(CODIGO)) TO WCODIGO
RETURN
****************
PROCEDURE BRELI2
****************
IF FILLOC()
   STORE WTOTFAC-TOTAL TO WTOTFAC
   DELETE
   UNLOCK ALL
ENDIF
RETURN
*****************
PROCEDURE VENDIDO
*****************
ACTI WIND BRSHOW
GO BOTT
IF .NOT.EOF().AND.RECNO()>10
   SKIP - 9
ELSE
   GO TOP
ENDIF
BROWSE FIELDS SYSART.CODIGO:H="CODIGO",SYSART.DESCRI:H="Articulo",CANTIDAD:H="Cantidad",PRECIO:H="Precio",TOTAL:H="Subtotal";
       NOWAIT
RETURN
****************
PROCEDURE SETPRI
****************
IF PTODATA.TIPPORT="S"
   *** IMPRIME SERIAL
   IF PTODATA.NUMPORT="1"
      !c:\dos\mode lpt1=com1>NULL.TXT
      !c:\dos\mode com1 4800,N,8,2,R>NULL.TXT
      ELSE
      !c:\dos\mode lpt1=com2>NULL.TXT
      !c:\dos\mode com2 4800,N,8,2,R>NULL.TXT
   ENDIF
ELSE
   *** IMPRIME PARALELO
   IF PTODATA.NUMPORT="1"
      !c:\dos\mode lpt1>NULL.TXT
   ELSE
      !c:\dos\mode lpt2>NULL.TXT
   ENDIF
ENDIF
RETURN
******************
PROCEDURE RESETPRI
******************
IF PTODATA.TIPPORT="S"
   !c:\dos\mode lpt1>NULL.TXT
   !c:\dos\mode lpt2>NULL.TXT
ENDIF
RETURN
******************
PROCEDURE ARTICULO
******************
librart=5
cibrart=0
SELECT SYSART
SET ORDER TO SYSART3
do sysart
SET ORDER TO SYSART1
STORE CODIGO TO WCODIGO
RETURN
